package [%=root.sysmini_child.first().sysmini_self%] {
[%for (e in root.sysmini_child.first().sysmini_child){%]
[%=e.getContent("", 1)%]
[%}%]
}

[%
operation Element getContent(content: String, layer: Integer){
	var typeName = self.type().name;
	content += self.getTab(layer);
	
//	Self
	if (typeName == "Package"){
		content += "Package " + self.sysmini_self;
	}
	else if (typeName == "Comment"){
		content += "/* "+self.sysmini_text+" */";
	}
	else if (typeName == "NamespaceImport"){
		content += self.getVisibility() + " import "+self.sysmini_self;
	}
	else if (typeName == "PartDefinition"){
		content += "part def " + self.sysmini_self;
	}
	else if (typeName == "AttributeUsage"){
		content += "attribute " + self.getTriplet();
	}
	else if (typeName == "Documentation"){
		content += "doc\n";
		content += self.getTab(layer)+"/*\n";
		content += self.getTab(layer)+" * "+self.sysmini_text+"\n";
		content += self.getTab(layer)+" */";
	}
	else if (typeName == "InterfaceDefinition"){
		content += "interface def " + self.sysmini_self;
	}
	else if (typeName == "ReferenceUsage"){
		content += self.getDirection() + " " + self.getTriplet();
	}
	else if (typeName == "PortUsage"){
		content += "port " + self.getTriplet();
	}
	else if (typeName == "FlowConnectionUsage"){
		content += "flow "+self.sysmini_source+" to "+self.sysmini_target;
	}
	else if (typeName == "PortDefinition"){
		content += "port def " + self.sysmini_self;
	}
	else {
		content += "!XXXX!";
	}
	
//	Children
	if (self.sysmini_child.size>0){
		content += " {\n";
		for (child in self.sysmini_child){
			content = child.getContent(content, layer+1);
			content += "\n";
		}
		content += self.getTab(layer)+"}";
	}
	else if (typeName != "Comment" and typeName != "Documentation"){
		content += ";";
	}

	return content;

}

operation Element getTab(layer : Integer){
	var tab = "";
	for (i in 1.to(layer)){
		tab += "\t";
	}
	return tab;
}

operation Element getVisibility(){
	if (self.sysmini_visibility){
		return "public";
	}
	else{
		return "private";
	}
}

operation Element getDirection(){
	return self.sysmini_direction;
}

operation Element getRelationship(){
	if (self.sysmini_relationship =="defined"){
		return ":";
	}
	else if (self.sysmini_relationship =="subsets"){
		return ":>";
	}
	else {
		return "";
	}
}

operation Element getTriplet(){
	return self.sysmini_self +" "+ self.getRelationship() +" "+ self.sysmini_object;
}

%]