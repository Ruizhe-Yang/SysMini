operation Ecore!EObject addRootNodeTo(rootNode : SysMini!Element){
	if (self.isEntity()){
		var node = self.equivalent();
		rootNode.body.add(node);
		
		node.addNamespaceImport("ecore");
		var namespaceImportPackage = Sequence{"ecore", node.declaredName};
		for (eReference in Ecore!EReference){
			if (eReference.isExtendedEcore(namespaceImportPackage)){
				var newNamespaceImport = eReference.eType.eContainer.name;
				node.addNamespaceImport(newNamespaceImport);
				namespaceImportPackage.add(newNamespaceImport);			
			}
		}
		
		for (child in self.eContents()){
			child.addNodeTo(node);
		}
	}
}

operation Ecore!EReference isExtendedEcore(namespaceImportPackage : Sequence){
	var eReferenceName = self.eType.eContainer().name.asString();
	if (namespaceImportPackage.includes(eReferenceName)){
		return false;
	}
	else{
		return true;
	}
}

operation SysMini!Package addNamespaceImport(name:String){
	var namespaceImport = new SysMini!NamespaceImport;
	namespaceImport.declaredName = name;
	self.body.add(namespaceImport);
}

operation Ecore!EObject addNodeTo(rootNode : SysMini!Element){
	if (self.isEntity()){
		var node = self.equivalent();
		rootNode.body.add(node);
		for (child in self.eContents()){
			child.addNodeTo(node);
		}
	}
}

operation Ecore!EObject isEntity(){
	if (self.isTypeOf(Ecore!EGenericType) or
		self.isTypeOf(Ecore!EOperation) or  
		self.isTypeOf(Ecore!EAnnotation)
		){
		return false;
	}
	else{
		return true;
	}
}

operation Ecore!ETypedElement getRange(){
	if (self.lowerBound == 0 and self.upperBound == 1){
		return "[1]";
	}
	else if (self.lowerBound == 0 and self.upperBound == -1){
		return "[*]";
	}
	else if (self.upperBound == -1){
		return "["+self.lowerBound+"..*]";
	}
	else if (self.lowerBound == self.upperBound){
		return "["+self.lowerBound+"]";
	}
	else if (self.lowerBound < self.upperBound){
		return "["+self.lowerBound+".."+self.upperBound+"]";
	}
	return "";
}

operation Sequence getElementOf(typeName : String){
	return self.select(e|e.type().name == typeName);
}

operation String clearKeyWord(){
	if (KeyWords.includes(self)){
		return "'"+self+"'";
	}
	else{
		return self;
	}
}